plugins {
    kotlin("jvm")

//    id("com.gradle.plugin-publish") version "1.2.1"

    `java-gradle-plugin`

    id("convention.publication")
}

group = findProperty("project.group") as String
version = findProperty("project.version") as String

repositories {
    mavenLocal()
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    implementation("org.jetbrains.kotlin:kotlin-gradle-plugin:2.2.21")
    implementation("org.jetbrains.kotlin:kotlin-allopen:2.2.21")

    implementation("com.android.tools.build:gradle:8.5.2")

    implementation("com.google.devtools.ksp:symbol-processing-gradle-plugin:2.2.21-2.0.4")
}

kotlin {
    jvmToolchain(11)
}

val copySourcesToResources by tasks.registering(Copy::class) {
    from("$rootDir/mockative-prefab/src")
    into("src/main/resources/src/")
}

tasks.named("processResources") {
    dependsOn(copySourcesToResources)
}

tasks.whenObjectAdded {
    if (name == "sourcesJar") {
        dependsOn(copySourcesToResources)
    }
}

gradlePlugin {
    website = "https://github.com/mohamadjaara/mockative"
    vcsUrl = "https://github.com/mohamadjaara/mockative.git"

    plugins {
        create("mockative") {
            id = "io.github.mohamadjaara.mockative"
            displayName = "Mockative Gradle Plugin (Fork)"
            description = "Gradle Plugin for Mockative (Fork)"
            tags = listOf("testing", "mocking", "mock", "kmp", "native")
            implementationClass = "io.mockative.MockativePlugin"
        }
    }
}
